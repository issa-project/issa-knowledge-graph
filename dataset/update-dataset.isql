log_enable(3,1);

-- Update number of triples
SPARQL 
PREFIX void:   <http://rdfs.org/ns/void#> 
PREFIX issa:   <http://data-issa.cirad.fr/>
PREFIX sd:   <http://www.w3.org/ns/sparql-service-description#>

WITH <http://data-issa.cirad.fr/graph/dataset>
DELETE { issa:issa-agritrop void:triples ?triples_old . }
INSERT { issa:issa-agritrop void:triples ?triples_new . }
WHERE {
    issa:issa-agritrop void:triples ?triples_old . 

	{ SELECT (COUNT(*) as ?triples_new) 
	  WHERE { 
 		GRAPH ?g {?s ?p ?o} 
   
  		{ SELECT ?g 
		  WHERE { ?dataset sd:availableGraphs [ sd:namedGraph ?g] . }
  		}  
   }} 
  }

;



-- Update dataset version
SPARQL 
PREFIX issa: <http://data-issa.cirad.fr/>
PREFIX owl:    <http://www.w3.org/2002/07/owl#>

WITH <http://data-issa.cirad.fr/graph/dataset>
DELETE { issa:issa-agritrop owl:versionInfo ?version_old . }
INSERT { issa:issa-agritrop owl:versionInfo "$ARGV[$I]" .  }
WHERE  { issa:issa-agritrop owl:versionInfo ?version_old . }

;


-- Update dataset dates
SPARQL 

PREFIX issa: <http://data-issa.cirad.fr/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#> 
PREFIX dct: <http://purl.org/dc/terms/>

WITH <http://data-issa.cirad.fr/graph/dataset>
DELETE { issa:issa-agritrop prov:wasGeneratedAtTime ?date_old .
         issa:issa-agritrop dct:modified ?date_old }
INSERT { issa:issa-agritrop prov:wasGeneratedAtTime ?date_new .
         issa:issa-agritrop dct:modified ?date_new}
WHERE {
    issa:issa-agritrop prov:wasGeneratedAtTime ?date_old . 
    BIND (xsd:date(NOW()) as ?date_new)
  }
;


-- Update linked sets
SPARQL 
PREFIX void:   <http://rdfs.org/ns/void#> 
PREFIX issa:   <http://data-issa.cirad.fr/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX oa: <http://www.w3.org/ns/oa#>

DELETE { GRAPH <http://data-issa.cirad.fr/graph/dataset> { ?linked_set void:triples ?triples_old . } }
INSERT { GRAPH <http://data-issa.cirad.fr/graph/dataset> { ?linked_set void:triples ?triples_new . } }

WHERE 
{
  ?linked_set a void:Linkset; void:triples ?triples_old.
  
  { SELECT (issa:issa2wikidata as ?linked_set) (COUNT(*) as ?triples_new) 
    WHERE { ?ann prov:wasAttributedTo issa:EntityFishing; oa:hasBody ?link. }
  } 
  UNION
  { SELECT (issa:issa2dbpedia as ?linked_set) (COUNT(*) as ?triples_new) 
    WHERE { ?ann prov:wasAttributedTo issa:DBPediaSpotlight; oa:hasBody ?link. }
  } 
  UNION
  { SELECT (issa:issa2geonames as ?linked_set) (COUNT(*) as ?triples_new) 
    WHERE { ?ann prov:wasAttributedTo issa:GeographicEntityExtractor; oa:hasBody ?link. }
  }
  UNION
  { SELECT (issa:issa2agrovoc as ?linked_set) (COUNT(*) as ?triples_new) 
    WHERE { ?ann a issa:ThematicDescriptorAnnotation; oa:hasBody ?link.	}
  } 
}
;


exit;



