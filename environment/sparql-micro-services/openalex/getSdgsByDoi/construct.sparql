PREFIX bibo:   <http://purl.org/ontology/bibo/>
PREFIX issa:   <http://data-issa.cirad.fr/>
PREFIX issapr: <http://data-issa.cirad.fr/property/>
PREFIX oa:     <http://www.w3.org/ns/oa#>
PREFIX xsd:    <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
    ?annotation
        a                   issa:OpenAlexSdgAnnotation;
        oa:hasBody          ?sdgId;
        oa:hasTarget        ?documentUri;
        issapr:confidence   ?sdgScore;
        rdfs:label          ?annotationLabel;
    .
}
WHERE {
    [] api:sustainable_development_goals [
         api:display_name ?sdgLabel;
         api:id ?sdgIdOriginal;
         api:score ?sdgScore
    ].

    BIND(IRI(REPLACE(STR(?sdgIdOriginal), "^https://", "http://")) AS ?sdgId)
    BIND(CONCAT("SDG annotation for ", ?sdgLabel) AS ?annotationLabel)
    BIND(IRI({documentUri}) AS ?documentUri)

    BIND(IRI(CONCAT(
        "http://data-issa.cirad.fr/sdgAnnotation/",
        SHA1(CONCAT(
            {documentUri}, 
            STR(?sdgId)
        ))
        )
    ) AS ?annotation)
}
