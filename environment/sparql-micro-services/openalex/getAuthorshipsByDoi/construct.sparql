PREFIX bibo:   <http://purl.org/ontology/bibo/>
PREFIX dce:    <http://purl.org/dc/elements/1.1/>
PREFIX dct:    <http://purl.org/dc/terms/>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX gn:     <http://www.geonames.org/ontology#>
PREFIX issa:   <http://data-issa.cirad.fr/>
PREFIX issapr: <http://data-issa.cirad.fr/property/>
PREFIX xsd:    <http://www.w3.org/2001/XMLSchema#>
PREFIX wdt:    <http://www.wikidata.org/prop/direct/>

CONSTRUCT {

    ?documentUri
        dce:creator     ?authorName;
        dct:creator     ?authorId;
        bibo:authorList ?authorshipHead.

    ?authorship rdf:first ?authorId; rdf:rest ?restAuthor.

    ?authorId
        a               foaf:Person;
        foaf:name       ?authorName;
        rdfs:label      ?authorName;
        wdt:P496        ?authorOrcid;
        issapr:institutionList ?institutionHead.

    ?institution rdf:first ?institutionId; rdf:rest ?restInstitution.

    ?institutionId
        a               foaf:Organization;
        foaf:name       ?institutionName;
        rdfs:label      ?institutionName;
        wdt:P6782       ?institutionRorId;
        gn:countryCode  ?institutionCountryCode.
}
WHERE {
    BIND(IRI({documentUri}) AS ?documentUri)

    [] api:authorships ?authorshipHead.
    
    ?authorship 
        rdf:first [
            api:author         ?author;
            api:institutions   ?institutionHead;
            api:countries      ?country;
        ];
        rdf:rest ?restAuthor.

    ?institution
        rdf:first [
            api:id                ?institutionId;
            api:display_name      ?institutionName;
            api:ror               ?institutionRorId;      # Not optional since all institutions has a ROR id in OpenAlex
            api:country_code      ?institutionCountryCode;
        ];
        rdf:rest ?restInstitution.

    ?author
        api:id              ?authorId;
        api:display_name    ?authorName.
    optional { ?author api:orcid ?authorOrcid. }
}
