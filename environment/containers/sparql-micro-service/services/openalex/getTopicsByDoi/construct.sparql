PREFIX bibo:   <http://purl.org/ontology/bibo/>
PREFIX issa:   <http://data-issa.cirad.fr/>
PREFIX issapr: <http://data-issa.cirad.fr/property/>
PREFIX oa:     <http://www.w3.org/ns/oa#>
PREFIX xsd:    <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
    ?annotationTopicsOpenalex
        a issa:OpenAlexTopicAnnotation;
        oa:hasBody          ?topicId;
        oa:hasTarget        ?documentUri;
        issapr:confidence   ?topicScore;
        rdfs:label          ?annotationLabelTopics;
    .

    ?annotationSubfieldsOpenalex
        a issa:OpenAlexSubfieldAnnotation;
        oa:hasBody          ?subfieldId;
        oa:hasTarget        ?documentUri;
        issapr:confidence   ?topicScore;
        rdfs:label          ?annotationLabelSubfields;
    .

    ?annotationFieldsOpenalex
        a issa:OpenAlexFieldAnnotation;
        oa:hasBody          ?fieldId;
        oa:hasTarget        ?documentUri;
        issapr:confidence   ?topicScore;
        rdfs:label          ?annotationLabelFields;
    .

    ?annotationDomainsOpenalex
        a issa:OpenAlexDomainAnnotation;
        oa:hasBody          ?domainId;
        oa:hasTarget        ?documentUri;
        issapr:confidence   ?topicScore;
        rdfs:label          ?annotationLabelDomains;
    .
}
WHERE {
    [] api:topics [
         api:display_name ?topicLabel;
         api:id ?topicIdOriginal;
         api:score ?topicScore;
         api:subfield [api:display_name ?subfieldLabel; api:id ?subfieldIdOriginal];
         api:field [api:display_name ?fieldLabel; api:id ?fieldIdOriginal];
         api:domain [api:display_name ?domainLabel; api:id ?domainIdOriginal]
    ].


    BIND(IRI(?topicIdOriginal) AS ?topicId)
    BIND(IRI(?subfieldIdOriginal) AS ?subfieldId)
    BIND(IRI(?fieldIdOriginal) AS ?fieldId)
    BIND(IRI(?domainIdOriginal) AS ?domainId)

    BIND(CONCAT("Topic annotation for ", ?topicLabel) AS ?annotationLabelTopics)
    BIND(CONCAT("Subfield annotation for ", ?subfieldLabel) AS ?annotationLabelSubfields)
    BIND(CONCAT("Field annotation for ", ?fieldLabel) AS ?annotationLabelFields)
    BIND(CONCAT("Domain annotation for ", ?domainLabel) AS ?annotationLabelDomains)

    BIND(IRI({documentUri}) AS ?documentUri)


    BIND(IRI(CONCAT(
        "http://data-issa.cirad.fr/topicAnnotation/",
        SHA1(CONCAT(
            STR(?documentUri), 
            STR(?topicId)
        ))
        )
    ) AS ?annotationTopicsOpenalex)

    BIND(IRI(CONCAT(
        "http://data-issa.cirad.fr/subfieldAnnotation/",
        SHA1(CONCAT(
            STR(?documentUri), 
            STR(?subfieldId)
        ))
        )
    ) AS ?annotationSubfieldsOpenalex)

    BIND(IRI(CONCAT(
        "http://data-issa.cirad.fr/fieldAnnotation/",
        SHA1(CONCAT(
            STR(?documentUri), 
            STR(?fieldId)
        ))
        )
    ) AS ?annotationFieldsOpenalex)

    BIND(IRI(CONCAT(
        "http://data-issa.cirad.fr/domainAnnotation/",
        SHA1(CONCAT(
            STR(?documentUri), 
            STR(?domainId)
        ))
        )
    ) AS ?annotationDomainsOpenalex)


}
