PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX pro:     <http://purl.org/spar/pro/>
PREFIX bibo:    <http://purl.org/ontology/bibo/>
PREFIX dct:     <http://purl.org/dc/terms/>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX dce:     <http://purl.org/dc/elements/1.1/>

SELECT ?s ?o ?p ?date ?label (concat("http://issa.i3s.unice.fr/notice/?uri=", ?p) as ?url) WHERE {
    ?p dce:creator ?s, ?o ;
        dct:title ?label ;
        dct:issued ?date .
    filter (?s != ?o)
   
   # Select only the most frequent couples of authors
   { select (count(distinct ?p) as ?n) ?s ?o  where {
        ?p  dce:creator ?s, ?o.
        filter (?s != ?o)
    } group by ?s ?o order by desc(?n) limit 2000 }    
}
