# Run with $value1  = Agricultural and Biological Sciences

PREFIX dct:     <http://purl.org/dc/terms/>
PREFIX fabio:   <http://purl.org/spar/fabio/>
PREFIX issa:    <http://data-issa.cirad.fr/>
PREFIX issapr:  <http://data-issa.cirad.fr/property/>
PREFIX oa:      <http://www.w3.org/ns/oa#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema:  <http://schema.org/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?uri ?title ?date ?type ?link ?ego ?alter  
FROM <http://data-issa.cirad.fr/graph/documents>
FROM <http://data-issa.cirad.fr/graph/openalex-topics>
FROM <http://data-issa.cirad.fr/graph/openalex-sdgs>
FROM <http://data-issa.cirad.fr/graph/sdgs-metadata>
FROM <http://data-issa.cirad.fr/graph/openalex-topics-hierarchy>
WHERE {
    ?uri dct:title ?title ;
         dct:issued ?date ;
         schema:url ?link ;
         a ?documentType .
    FILTER(?date > "2000"^^xsd:gYear)

    ?annotation1 
        oa:hasTarget ?uri ;
        oa:hasBody ?field1 ;
        a issa:OpenAlexFieldAnnotation .
    ?field1 skos:prefLabel ?ego .
    FILTER(?ego = "$value1")

    OPTIONAL {
        ?annotation2 
            oa:hasTarget ?uri ;
            oa:hasBody ?field2 ;
            a issa:OpenAlexFieldAnnotation .
        ?field2 skos:prefLabel ?alter .
    }

    ?annotationSdg 
        oa:hasTarget ?uri ;
        oa:hasBody ?sdg;
        a issa:OpenAlexSdgAnnotation .
    ?sdg skos:altLabel ?sdgLabel .
    FILTER (lang(?sdgLabel) = "en")
  
    BIND (
        # Climate action ; Life below water ; Life on land
        IF (?sdg in (<http://metadata.un.org/sdg/13>, <http://metadata.un.org/sdg/14>, <http://metadata.un.org/sdg/15>), "Environment",
        # Zero hunger ; Good health and well-being ; Clean water and sanitation
        IF (?sdg in (<http://metadata.un.org/sdg/2>, <http://metadata.un.org/sdg/3>, <http://metadata.un.org/sdg/6>, <http://metadata.un.org/sdg/7>), "Basic Needs",
        # Decent work and economic growth ; Industry, innovation and infrastructure ; Sustainable cities and communities
        IF (?sdg in (<http://metadata.un.org/sdg/8>, <http://metadata.un.org/sdg/9>, <http://metadata.un.org/sdg/11>), "Economic Growth",
        # No poverty, Quality education, Gender equality, Reduced inequalities
        IF (?sdg in (<http://metadata.un.org/sdg/1>, <http://metadata.un.org/sdg/4>, <http://metadata.un.org/sdg/5>, <http://metadata.un.org/sdg/10>), "Social Equality",
        # Responsible consumption and production
        IF (?sdg = <http://metadata.un.org/sdg/12>, "Consumption",
        # Peace, justice and strong institutions
        IF (?sdg = <http://metadata.un.org/sdg/16>, "Governance", 
        # Partnerships for the goals
      	"Partnerships")))))) AS ?type)

} LIMIT 5000
